#!/usr/bin/perl
use warnings;
use strict;
use Time::Piece;
use POSIX;
use JSON;

sub ParseConfig($);

sub main;
sub dispatch;
sub clear;
sub buildopts;
sub change_account_detail;
sub change_account_detail_aix;
sub change_account_detail_linux;
sub change_account_expire;
sub change_account_expire_linux;
sub create_admin_account;
sub create_sftp_account_linux;
sub create_user_account;
sub delete_account;
sub get_users;
sub get_users_aix;
sub get_users_linux;
sub reset_password_aix;
sub reset_password_linux;
sub select_user;
sub unlock_account_aix;
sub unlock_account_linux;
sub unselect_user;
sub view_account_detail_aix;
sub view_account_detail_linux;
sub view_all_users;

# Parse stanzas into a hash of hashes
sub parsestanzas;
sub quit;

main();

sub ParseConfig($) {
    my $location = $_[0];
    my $data;
    local $/ = undef;
    open(my $file, '<', $location) or return {menu => []};
    binmode $file;
    my $json = <$file>;
    return decode_json($json);
}

sub main {
    my $config = ParseConfig('/etc/manage_users.json');
    $config->{exit} = 0;
    my %data;

    my %allroutines = (
        change_account_detail => 'Change User Account Details',
        change_account_expire => 'Change User Account Password Expiry',
        create_admin_account => 'Create Admin Account',
        create_sftp_account => 'Create Client Account',
        create_user_account => 'Create User Account',
        delete_account => 'Remove User Account',
        quit => 'Exit',
        reset_password => 'Reset Password and Unlock Account',
        select_user => 'Select User',
        unlock_account => 'Unlock Account Without Resetting Password',
        unselect_user => 'Unselect User',
        view_account_detail => 'View User Account Details',
        view_all_users => 'View All Users'
    );
    while (!$config->{exit})
    {
        clear();
        my @routines;

        for my $item (@{$config->{menu}})
        {
            if (exists($allroutines{$item}))
            {
                push(@routines, $item);
            } else
            {
                print("Could not find requested routine $item!  You are missing functionality!\n");
            }
        }
        push(@routines, 'quit');

        clear();

        if (exists($data{user})) {
            print "Selected User:  $data{user}->{name} => $data{user}->{comment}\n";
            print "------------------------------------------------------------------\n\n";
        } else {
            print "No user selected!  Please create or select a user to proceed\n";
            print "------------------------------------------------------------------\n\n";
        }

        # print routines
        for (my $i = 0; $i < scalar(@routines); $i++)
        {
            my $routine = $routines[$i];
            print(($i + 1) . ") " . $allroutines{$routine} . "\n");
        }

        print "------------------------------------------------------------------\n\n";
        print "Select Option(default: 1): ";

        my $opt = <>;
        chomp $opt;

        $opt ||= 1;

        # if (input is a digit) && (input is > 1) && (input does not exceed list length)
        if (($opt =~ m/^\d+$/) && ($opt > 0) && ($opt <= scalar(@routines)))
        {
            # options are listed starting at 1, but list is 0-indexed
            $opt--;

            my $routine = $routines[$opt];

            dispatch($routine, \%data, $config);
        }
    }
}

sub dispatch {
    my ($function, $data, $config) = @_;
    my %dispatch = (
        change_account_detail => {AIX => \&change_account_detail_aix, Linux => \&change_account_detail_linux},
        change_account_expire => {Linux => \&change_account_expire_linux},
        create_admin_account => {DEFAULT => \&create_admin_account},
        create_sftp_account => {Linux => \&create_sftp_account_linux},
        create_user_account => {DEFAULT => \&create_user_account},
        delete_account => {DEFAULT => \&delete_account},
        reset_password => {AIX => \&reset_password_aix, Linux => \&reset_password_linux},
        select_user => {DEFAULT => \&select_user},
        unlock_account => {AIX => \&unlock_account_aix, Linux => \&unlock_account_linux},
        unselect_user => {DEFAULT => \&unselect_user},
        view_account_detail => {AIX => \&view_account_detail_aix, Linux => \&view_account_detail_linux},
        view_all_users => {DEFAULT => \&view_all_users},
        quit => {DEFAULT => \&quit},
    );
    my %needs = (
        change_account_detail => [qw(user)],
        change_account_expire => [qw(user)],
        delete_account => [qw(user)],
        reset_password => [qw(user)],
        unlock_account => [qw(user)],
        unselect_user => [qw(user)],
        view_account_detail => [qw(user)]
    );

    my $os = $config->{OS};

    if (exists($dispatch{$function}))
    {
        if (exists($needs{$function}))
        {
            for my $need (@{$needs{$function}})
            {
                return if (not exists($data->{$need}));
            }
        }
        if (exists($dispatch{$function}->{$os}))
        {
            $dispatch{$function}->{$os}->($data, $config);
        } elsif (exists($dispatch{$function}->{DEFAULT}))
        {
            $dispatch{$function}->{DEFAULT}->($data, $config);
        }
    }
}

sub clear {
    print("\n" x 200);
}

sub buildopts {
    my $opts = $_[0];
    my @singles;
    my @argopts;
    my @args;

    for my $opt (@{$opts})
    {
        if (ref($opt) eq 'ARRAY')
        {
            if (scalar(@{$opt} > 1))
            {
                # Make sure all single-quotes are escaped and everything else is properly captured
                my $optarg = $opt->[1];
                $optarg =~ s/'/'"'"'/g;
                push(@argopts, "-$opt->[0]", "'$optarg'");
            } else
            {
                push(@singles, $opt->[0]);
            }
        } else
        {
            my $arg = $opt;
            $arg =~ s/'/'"'"'/g;
            push(@args, "'$arg'");
        }
    }
    my @outstrings;

    if (scalar(@singles) > 0)
    {
        push(@outstrings, '-' . join('', @singles));
    }

    push(@outstrings, @argopts);
    push(@outstrings, @args);
    return join(' ', @outstrings);
}

sub get_users {
    my ($config) = @_;
    my %dispatch = (
        AIX => \&get_users_aix,
        Linux => \&get_users_linux
    );

    if (exists($dispatch{$config->{OS}}))
    {
        $dispatch{$config->{OS}}->();
    } else
    {
        print("There is no get_users function for OS $config->{OS}, this is a bug in the program or deployment.\n");
    }
}

# AIX get users
sub get_users_aix {

    my @users;
    my %userrefs;

    my %secpasswd = parsestanzas('/etc/security/passwd');
    my %secuser = parsestanzas('/etc/security/user');

    setpwent();
    while (my @pw = getpwent())
    {
        my %user;
        ($user{name}, $user{password}, $user{uid}, $user{gid}, $user{quota}, $user{comment}, $user{gcos}, $user{home}, $user{shell}, $user{expire}) = @pw;
        $user{comment} ||= $user{gcos};

        $user{pgrp} = (getgrgid($user{gid}))[0];
        $user{groups} = [];

        # We set userrefs so that we can use the grent functions to fill the groups field idiomatically
        $userrefs{$user{name}} = \%user;

        if (defined($secpasswd{$user{name}}) && defined($secpasswd{$user{name}}{lastupdate}) && ($secpasswd{$user{name}}{lastupdate} =~ m/^\d+$/))
        {
            $user{lastchange} = $secpasswd{$user{name}}{lastupdate} / (60 * 60 * 24);
        } else
        {
            $user{lastchange} = '';
        }

        if (defined($secuser{$user{name}}) && defined($secuser{$user{name}}{maxage}) && ($secuser{$user{name}}{maxage} =~ m/^\d+$/))
        {
            $user{maxage} = $secuser{$user{name}}{maxage};
        } else
        {
            $user{maxage} = 0;
        }

        # Day of password expiration
        $user{expirationdate} = '';
        if ($user{maxage} > 0)
        {
            # Calculate the day of expiration
            $user{expirationdate} = (($user{maxage} * 7) + $user{lastchange})
        }

        $user{reserved} = '';
        push(@users, \%user);
    }
    endpwent();

    setgrent();
    while (my @gr = getgrent())
    {
        for my $member (split(/\s+/, $gr[3]))
        {
            unless ($gr[0] eq $userrefs{$member}->{pgrp})
            {
                push(@{$userrefs{$member}->{groups}}, $gr[0]);
            }
        }
    }
    endgrent();

    return @users;
}

sub get_users_linux {
    my @users;
    my %userrefs;

    my @shadowLines = split(/\n/, `cat /etc/shadow`);
    chomp(@shadowLines);

    setpwent();
    while (my @pw = getpwent())
    {
        my %user;
        ($user{name}, $user{password}, $user{uid}, $user{gid}, $user{quota}, $user{comment}, $user{gcos}, $user{home}, $user{shell}, $user{expire}) = @pw;
        $user{comment} ||= $user{gcos};

        $user{pgrp} = (getgrgid($user{gid}))[0];
        $user{groups} = [];

        # We set userrefs so that we can use the grent functions to fill the groups field idiomatically
        $userrefs{$user{name}} = \%user;
        my $shadow = (grep(/^$user{name}:/, @shadowLines))[0];
        my $dummy;
        if (defined($shadow))
        {
            ($dummy, $user{password}, $user{lastchange}, $user{minage}, $user{maxage}, $user{warningperiod}, $user{inactivityperiod}, $user{expirationdate}, $user{reserved}) = split(/:/, $shadow);
        } else
        {
            $user{password} = '';
            $user{lastchange} = '';
            $user{minage} = '';
            $user{maxage} = '';
            $user{warningperiod} = '';
            $user{inactivityperiod} = '';
            $user{expirationdate} = '';
            $user{reserved} = '';
        }
        push(@users, \%user);
    }
    endpwent();
    
    setgrent();
    while (my @gr = getgrent())
    {
        for my $member (split(/\s+/, $gr[3]))
        {
            unless ($gr[0] eq $userrefs{$member}->{pgrp})
            {
                push(@{$userrefs{$member}->{groups}}, $gr[0]);
            }
        }
    }
    endgrent();
    return @users;
}

sub select_user {
    my ($data, $config) = @_;
    print "\nEnter username or search string: ";
    my $search = <>;
    chomp($search);
    my $regex = qr/$search/;

    setpwent();
    my @matches;
    setpwent();
    while(my ($name, $passwd, $uid, $gid, $quota, $comment, $gcos, $dir, $shell) = getpwent())
    {
        if ($name =~ $regex or $uid =~ $regex or $comment =~ $regex or $gcos =~ $regex)
        {
            push(@matches, {name => $name, password => $passwd, uid => $uid, gid => $gid, comment => ($gcos or $comment), home => $dir, shell => $shell});
        }
    }
    endpwent();
    clear();

    if (scalar(@matches) > 0)
    {
        my %menu = (
            Name => 'Menu',
            Select => 'One',
            Banner => 'Choose a user',
            Display => 25,
            Item_1 => {
                Text => ']Convey[',
                Convey => []
            },
        );

        my %backsearch;

        for (my $i = 0; $i < scalar(@matches); ++$i)
        {
            my $match = $matches[$i];

            print "$i) $match->{name}\t$match->{comment}\n";
        }
        print "Select user index: ";
        my $usr_index = <>;
        chomp $usr_index;
        my $user = $matches[$usr_index];
        $data->{user} = $user;

        $user->{pgrp} = getgrgid($user->{gid});
        $user->{groups} = [];
        setgrent();
        while (my ($name, $passwd, $gid, $members) = getgrent())
        {
            my $username = $user->{name};

            if (grep(/^$username$/, split(' ', $members)))
            {
                push(@{$user->{groups}}, $name);
            }
        }
        endgrent();
    } else
    {
        print "NO MATCHES FOUND FOR SEARCH OF '$search'\n";
        print "\n\nPress enter to continue";
        my $ret = <>;
    }
}

sub view_all_users {
    my ($data, $config) = @_;

    my @users;

    @users = get_users($config);

    my @sortoptions = (['name', 'User Name'], ['comment', 'Comment Field'], ['lastchange', 'Last Password Change'], ['expirationdate', 'Days Until Expiration'], ['uid', 'User ID'], ['pgrp', 'Primary Group'], ['maxage', 'Max Password Age']);
    my $option;
    while ((!defined($option)) or (length($option) == 0) or (($option < 0) or ($option > $#sortoptions)))
    {
        clear();
        for my $i (0..$#sortoptions)
        {
            print("$i) $sortoptions[$i]->[1]\n");
        }
        print("Select sort field: ");
        $option = <>;
        chomp($option);
    }
    my $sort = $sortoptions[$option]->[0];
    my $sortdesc = $sortoptions[$option]->[1];
    my @susers = sort {
            if (((!exists($a->{$sort})) or (length($a->{$sort}) == 0)) and ((!exists($b->{$sort})) or (length($b->{$sort}) == 0)))
            {
                return 0;
            } elsif ((!exists($a->{$sort})) or (length($a->{$sort}) == 0))
            {
                return -1;
            } elsif ((!exists($b->{$sort})) or (length($b->{$sort}) == 0))
            {
                return 1;
            } else
            {
                if (($a->{$sort} =~ m/^\d+$/) and ($b->{$sort} =~ m/^\d+$/))
                {
                    return $a->{$sort} <=> $b->{$sort};
                } else
                {
                    return $a->{$sort} cmp $b->{$sort};
                }
            }
        } @users;

    $option = undef;
    print("Would you like to reverse the sort? <y/n>: ");
    $option = <>;
    chomp($option);
    if ($option =~ m/^y/i)
    {
        @susers = reverse(@susers);
    }

    clear();

    printf("%15.15s %25.25s %25.25s\n", 'Username', 'Comment', $sortdesc);
    printf("%15.15s %25.25s %25.25s\n", '--------', '-------', '-' x length($sortdesc));

    # Current date in days since the epoch
    my $date = int(time/86400);
    for my $user (@susers)
    {
        my %hash = %{$user};
        $hash{lastchange} = ((length($user->{lastchange}) > 0) ? sprintf('%.2f days ago', $date - $user->{lastchange}) : 'N/A');
        $hash{expirationdate} = ((length($user->{expirationdate}) > 0) ? sprintf('%.2f', $date - $user->{expirationdate}) : 'Never');
        $hash{maxage} = ((length($user->{maxage}) > 0) ? $user->{maxage} : 'None');
        printf("%15.15s %25.25s %25.25s\n", $hash{name}, $hash{comment}, $hash{$sort});
    }

    printf("%15.15s %25.25s %25.25s\n", '--------', '-------', '-' x length($sortdesc));
    printf("%15.15s %25.25s %25.25s\n", 'Username', 'Comment', $sortdesc);

    print "\n\nPress enter to continue";
    my $ret = <>;
}

sub view_account_detail_aix {
    my ($data, $config) = @_;
    my $user = $data->{user};
    my @lsuser = split(/ /,`lsuser $user->{name}`);
    my %userdetail;
    foreach my $user (@lsuser) {
        chomp;
        my @el = split(/=/,$user);
        if ($#el > 0) {
            $userdetail{$el[0]} = $el[1];
        }
    }
    clear();
    printf("%-40s%s\n", 'username:', $user->{name});
    printf("%-40s%s\n", 'primary group:', $userdetail{'pgrp'});
    printf("%-40s%s\n", 'groups:', $userdetail{'groups'});
    printf("%-40s%s\n", 'shell:', $userdetail{'shell'});
    printf("%-40s%s\n", 'home:', $userdetail{'home'});
    printf("%-40s%s\n", 'login:', $userdetail{'login'});
    printf("%-40s%s\n", 'account locked:', $userdetail{'account_locked'});
    printf("%-40s%s\n", 'unsuccessful login count:', defined($userdetail{'unsuccessful_login_count'}) ? $userdetail{'unsuccessful_login_count'} : 0);
    printf("%-40s%s\n", 'last successful login:', defined($userdetail{'time_last_login'}) ? strftime('%F %T', localtime($userdetail{'time_last_login'})) : 'never');
    printf("%-40s%s\n", 'last unsuccessful login:', defined($userdetail{'time_last_unsuccessful_login'}) ? strftime('%F %T', localtime($userdetail{'time_last_unsuccessful_login'})) : 'never');

    print("\nPress Enter to Continue\n");
    my $v = <>;

    return;
}

sub view_account_detail_linux {
    my ($data, $config) = @_;
    my $user = $data->{user};
    clear();
    printf("%-20s%s\n", 'username:', $user->{name});
    printf("%-20s%s\n", 'pgroup:', $user->{pgrp});
    printf("%-20s%s\n", 'groups:', join(', ', @{$user->{groups}}));
    printf("%-20s%s\n", 'uid:', $user->{uid});
    printf("%-20s%s\n", 'gid:', $user->{gid});
    printf("%-20s%s\n", 'details:', $user->{comment});
    printf("%-20s%s\n", 'home:', $user->{home});
    printf("%-20s%s\n", 'shell:', $user->{shell});
    print(`chage -l $user->{name}`);

    print("\nPress Enter to Continue\n");
    my $v = <>;

    return;
}

sub change_account_detail_aix {
    my ($data, $config) = @_;
    my $user = $data->{user};
    clear();

    my @lsuser = split(/ /,`lsuser $user->{name}`);
    my $username = shift @lsuser;
    my %userdetail;

    foreach my $user (@lsuser) {
        chomp($user);
        my @el = split(/=/,$user);
        if ($#el > 0) {
            $userdetail{$el[0]} = $el[1];
        }
    }

    my %details;

    my $correct = "N";

    while ( $correct !~ /[Yy]/ ) {
        %details = %userdetail;
        my $input;
        print "Modify account $username:\n";
        print "Enter nothing to keep default\n";
        print "primary group [$details{pgrp}]: ";
        $input = <>;  chomp $input;
        $details{pgrp} = $input if ($input ne "");
        print "groups [$details{groups}]: ";
        $input = <>;  chomp $input;
        $details{groups} = $input if ($input ne "");
        print "shell [$details{shell}]: ";
        $input = <>;  chomp $input;
        $details{shell} = $input if ($input ne "");
        print "home [$details{home}]:\n";
        $input = <>;  chomp $input;
        $details{home} = $input if ($input ne "");
        print "-----------------------------------------------------------\n\n";
        print "username:                 $username\n";
        print "primary group:            $details{'pgrp'}\n";
        print "groups:                   $details{'groups'}\n";
        print "shell:                    $details{'shell'}\n";
        print "home:                     $details{'home'}\n";
        print "-----------------------------------------------------------\n\n";
        print "Is this correct? (Y|N) ";
        $correct = <>;  chomp $correct;
    }  
    system("sudo chuser 'pgrp=$details{pgrp}' 'groups=$details{groups}' 'shell=$details{shell}' 'home=$details{home}' $username");

    view_account_detail_aix(@_);
}

sub change_account_detail_linux {
    my ($data, $config) = @_;
    my $user = $data->{user};
    clear();

    my $correct = "N";

    my %details;

    while ( $correct !~ /[Yy]/ ) {
        %details = %{$user};
        $details{groups} = join(',', @{$user->{groups}});

        my $input;
        print "Modify account $details{name}\n";
        print "Enter nothing to keep default\n";
        print "details [$details{comment}]: ";
        $input = <>;  chomp $input;
        $details{comment} = $input if ($input ne "");
        print "primary group [$details{pgrp}]: ";
        $input = <>;  chomp $input;
        $details{pgrp} = $input if ($input ne "");
        print "groups [$details{groups}]: ";
        $input = <>;  chomp $input;
        $details{groups} = $input if ($input ne "");
        print "shell [$details{shell}]: ";
        $input = <>;  chomp $input;
        $details{shell} = $input if ($input ne "");
        print "home [$details{home}]:";
        $input = <>;  chomp $input;
        $details{home} = $input if ($input ne "");
        print "-----------------------------------------------------------\n\n";
        print "username:                 $details{name}\n";
        print "details:                  $details{comment}\n";
        print "primary group:            $details{pgrp}\n";
        print "groups:                   $details{groups}\n";
        print "shell:                    $details{shell}\n";
        print "home:                     $details{home}\n";
        print "-----------------------------------------------------------\n\n";
        print "Is this correct? (Y|N) ";
        $correct = <>;  chomp $correct;
    }
    system("usermod -c '$details{comment}' -d '$details{home}' -s '$details{shell}' -g '$details{pgrp}' -G '$details{groups}' $details{name}");

    $user->{comment} = $details{comment};
    $user->{home} = $details{home};
    $user->{shell} = $details{shell};
    $user->{pgrp} = $details{pgrp};
    @{$user->{groups}} = split(/,/, $details{groups});

    view_account_detail_linux(@_);
    return;
}

sub change_account_expire_linux {
    my ($data, $config) = @_;
    my $user = $data->{user};

    my $correct = 'N';

    my @opts;

    while ( $correct !~ /[Yy]/ ) {
        clear();

        @opts = ($user->{name});
        my $banner = '';

        my $input;
        print "Modify account $user->{name}\n";
        print "Enter nothing to keep old value (viewable from user account details)\n";

        print "Expire date (YYYY-MM-DD format.  -1 to disable): ";
        $input = <>;  chomp $input;
        push(@opts, ['E', $input]) if ($input ne "");
        $banner .= sprintf("%-50s%s\n", 'epire date:', $input) if ($input ne "");

        print "Number of days of inactivity after password expires to lock account (-1 to disable): ";
        $input = <>;  chomp $input;
        push(@opts, ['I', $input]) if ($input ne "");
        $banner .= sprintf("%-50s%s\n", 'inactivity days:', $input) if ($input ne "");

        print "Minimum number of days between password changes (0 to disable, and let password be changed at any time): ";
        $input = <>;  chomp $input;
        push(@opts, ['m', $input]) if ($input ne "");
        $banner .= sprintf("%-50s%s\n", 'minimum days between changes:', $input) if ($input ne "");

        print "Maximum number of days between password changes (-1 to disable, making a password never expire): ";
        $input = <>;  chomp $input;
        push(@opts, ['M', $input]) if ($input ne "");
        $banner .= sprintf("%-50s%s\n", 'maximum days between changes:', $input) if ($input ne "");

        print "Number of days before expiry to warn that a password is going to expire:  ";
        $input = <>;  chomp $input;
        push(@opts, ['W', $input]) if ($input ne "");
        $banner .= sprintf("%-50s%s\n", 'days to warn:', $input) if ($input ne "");

        $banner .= "\n";

        print($banner);
        print("Is this correct? (Y|N) ");
        $correct = <>;  chomp $correct;
    }
    system('chage ' . buildopts(\@opts));

    view_account_detail_linux(@_);
    return;
}

sub reset_password_aix {
    my ($data, $config) = @_;
    my $user = $data->{user};
    print "Resetting account: $user->{name} for " . (split(/,/, $user->{comment}))[0] . "\n";
    system("passwd $user->{name}");
    print "\nForce password change? (Y|N):";
    my $chpw = <>; chomp $chpw;
    if ($chpw =~ /[Nn]/) {
        system("sudo pwdadm -c $user->{name}");
    }
    unlock_account_aix(@_);
}

sub unlock_account_aix {
    my ($data, $config) = @_;
    my $user = $data->{user};
    print("Unlocking account: $user->{name} for " . (split(/,/, $user->{comment}))[0] . "\n");
    system("chuser unsuccessful_login_count=0 account_locked=false $user->{name}");
    print("\nUser Successfully Unlocked!\n");
    print("\nPress Enter to Continue\n");
    my $v = <>;
    return;
}
sub reset_password_linux {
    my ($data, $config) = @_;
    my $user = $data->{user};
    print "Resetting account: $user->{name} for $user->{comment}\n";
    system("passwd $user->{name}");
    unlock_account_linux(@_);
    return;
}

sub unlock_account_linux {
    my ($data, $config) = @_;
    my $user = $data->{user};
    print "Unlocking account: $user->{name} for $user->{comment}\n";
    system("passwd -u $user->{name}");
    system("chage -d " . localtime->strftime('%F') . " $user->{name}");
    print("\nUser Successfully Unlocked!\n");
    print("\nPress Enter to Continue\n");
    my $v = <>;
    return;
}

sub create_user_account {
    my ($data, $config) = @_;
    my $correct = "N";
    my ($username, $fullname, $phone, $location, $business);
    while ( $correct !~ /[Yy]/ ) {
        clear();
        print "Create new account:\n";
        print "-----------------------------------------------------------\n\n";
        print "Enter username: ";
        $username = <>; chomp $username;
        print "Enter Full Name: ";
        $fullname = <>; chomp $fullname;
        print "Enter Phone Number: ";
        $phone = <>; chomp $phone;
        print "Enter Location: ";
        $location = <>; chomp $location;
        print "Enter Business Unit: ";
        $business = <>; chomp $business;
        print "\n\n-----------------------------------------------------------\n\n";
        print "Please confim user details:\n";
        print "-----------------------------------------------------------\n\n";
        print "Username:  $username\n";
        print "Fullname:  $fullname\n";
        print "Phone:     $phone\n";
        print "Location:  $location\n";
        print "Business:  $business\n";
        print "-----------------------------------------------------------\n\n";
        print "Is this correct? (Y|N) ";
        $correct = <>;  chomp $correct;
    }  
    clear();
    print "Creating account $username for $fullname ...\n";

    my @opts;
    if ($config->{user}->{sharedhome})
    {
        push(@opts, ['d', $config->{user}->{home}]);
    } else
    {
        push(@opts, ['d', "$config->{user}->{home}/$username"]);
        push(@opts, ['m']);
    }

    if (exists($config->{user}->{groups}))
    {
        push(@opts, ['G', join(',', @{$config->{user}->{groups}})]);
    }

    push(@opts, ['g', $config->{user}->{pgrp}]);
    push(@opts, ['c', "$fullname,$phone,$location,$business,USER,USER"]);
    push(@opts, $username);

    system('useradd ' . buildopts(\@opts));

    system("passwd $username");
    print "\n\nAccount creation complete.\n";
    print "\n\nPress enter to continue";
    my $ret = <>;
}

sub create_admin_account {
    my ($data, $config) = @_;
    clear();
    my $correct = "N";
    my ($username, $fullname, $phone, $location, $business);
    while ( $correct !~ /[Yy]/ ) {
        print "Create new account:\n";
        print "-----------------------------------------------------------\n\n";
        print "Enter username: ";
        $username = <>; chomp $username;
        print "Enter Full Name: ";
        $fullname = <>; chomp $fullname;
        print "Enter Phone Number: ";
        $phone = <>; chomp $phone;
        print "Enter Location: ";
        $location = <>; chomp $location;
        print "Enter Business Unit: ";
        $business = <>; chomp $business;
        print "\n\n-----------------------------------------------------------\n\n";
        print "Please confim user details:\n";
        print "-----------------------------------------------------------\n\n";
        print "Username:  $username\n";
        print "Fullname:  $fullname\n";
        print "Phone:     $phone\n";
        print "Location:  $location\n";
        print "Business:  $business\n";
        print "-----------------------------------------------------------\n\n";
        print "Is this correct? (Y|N) ";
        $correct = <>;  chomp $correct;
    }  
    clear();
    print "Creating account $username for $fullname ...\n";

    my @opts;
    if ($config->{admin}->{sharedhome})
    {
        push(@opts, ['d', $config->{admin}->{home}]);
    } else
    {
        push(@opts, ['d', "$config->{admin}->{home}/$username"]);
        push(@opts, ['m']);
    }

    if (exists($config->{admin}->{groups}))
    {
        push(@opts, ['G', join(',', @{$config->{admin}->{groups}})]);
    }

    push(@opts, ['g', $config->{admin}->{pgrp}]);
    push(@opts, ['c', "$fullname,$phone,$location,$business,USER,USER"]);
    push(@opts, $username);

    system('useradd ' . buildopts(\@opts));

    system("passwd $username");
    print "\n\nAccount creation complete.\n";
    print "\n\nPress enter to continue";
    my $ret = <>;
}

sub create_sftp_account_linux {
    my ($data, $config) = @_;
    clear();
    my $correct = "N";
    my ($username, $fullname, $phone, $location, $business);
    while ( $correct !~ /[Yy]/ ) {
        print "Create new SFTP account:\n";
        print "-----------------------------------------------------------\n\n";
        print "Enter username: ";
        $username = <>; chomp $username;
        print "Enter Full Name: ";
        $fullname = <>; chomp $fullname;
        print "Enter Phone Number: ";
        $phone = <>; chomp $phone;
        print "Enter Location: ";
        $location = <>; chomp $location;
        print "Enter Business Unit: ";
        $business = <>; chomp $business;
        print "\n\n-----------------------------------------------------------\n\n";
        print "Please confim user details:\n";
        print "-----------------------------------------------------------\n\n";
        print "Username:  $username\n";
        print "Fullname:  $fullname\n";
        print "Phone:     $phone\n";
        print "Location:  $location\n";
        print "Business:  $business\n";
        print "-----------------------------------------------------------\n\n";
        print "Is this correct? (Y|N) ";
        $correct = <>;  chomp $correct;
    }  
    clear();
    print "Creating SFTP account $username for $fullname ...\n";

    my @opts;
    my $homedir;
    if ($config->{ftpuser}->{sharedhome})
    {
        $homedir = $config->{ftpuser}->{home};
        push(@opts, ['d', $homedir]);
    } else
    {
        $homedir = "$config->{ftpuser}->{home}/$username";
        push(@opts, ['d', $homedir]);
        push(@opts, ['m']);
    }

    if (exists($config->{ftpuser}->{groups}))
    {
        push(@opts, ['G', join(',', @{$config->{ftpuser}->{groups}})]);
    }

    my $ftpgrp = $config->{ftpuser}->{pgrp};

    push(@opts, ['g', $ftpgrp]);
    push(@opts, ['c', "$fullname,$phone,$location,$business,USER,USER"]);
    push(@opts, ['s', '/sbin/nologin']);
    push(@opts, $username);


    system('useradd ' . buildopts(\@opts));


    system('useradd ' . buildopts(\@opts));
    system('chown ' . buildopts(["root:$ftpgrp", $homedir]));
    system('chmod ' . buildopts([755, $homedir]));

    my $filesdir = "$homedir/files";

    system('mkdir ' . buildopts([$filesdir]));
    system('chown ' . buildopts(["$username:$ftpgrp", $filesdir]));
    system('chmod ' . buildopts([700, $filesdir]));
    system('setfacl ' . buildopts([['m', "u:$username:rwx"], $filesdir]));
    system('setfacl ' . buildopts([['d'], ['m', "u:$username:rwx"], $filesdir]));
    system('setfacl ' . buildopts([['m', "g:$ftpgrp:rwx"], $filesdir]));
    system('setfacl ' . buildopts([['d'], ['m', "g:$ftpgrp:rwx"], $filesdir]));
    system('setfacl ' . buildopts([['m', "g:$config->{admin}->{group}:rwx"], $filesdir]));
    system('setfacl ' . buildopts([['d'], ['m', "g:$config->{admin}->{group}:rwx"], $filesdir]));

    system("passwd $username");

    print "\n\nSFTP account creation complete.\n";
    print "\n\nPress enter to continue";
    my $ret = <>;
}

sub delete_account {
    my ($data, $config) = @_;
    my $user = $data->{user};
    clear();
    print "##################################################################\n";
    print "ABOUT TO REMOVE USER ACCCOUNT!!!\n";
    print "##################################################################\n\n";

    print "Enter Y to confirm account removal for user $user->{name}: ";
    my $con = <>; chomp $con;
    if ($con eq "Y") {
        system("userdel $user->{name}");
        print "\n\nUser: $user->{name} removed from $config->{OS}.\n";
        delete($data->{user});
    }
    else {
        print "\n\nYou did not enter Y, returning to main menu without removing user.\n";
    }
    print "\nPress enter to continue ";
    my $ret = <>;
}

sub unselect_user {
    my ($data, $config) = @_;
    my $username = $data->{user}->{name};
    delete($data->{user});
    print("Unselected user $username\n");
    print("\nPress enter to continue ");
    my $ret = <>;
}

sub parsestanzas {
    my $filename = $_[0];
    my %output;
    my $stanza = 'default';

    open(my $file, '<', $filename);
    while (my $line = <$file>)
    {
        chomp($line);
        if ($line =~ m/^([^\s#\*]+):\s*$/)
        {
            $stanza = $1;
        } elsif ($line =~ m/^\s+(\S+)\s*=\s*(.*)$/)
        {
            $output{$stanza}{$1} = $2;
        }
    }

    return %output;
}

sub quit {
    my ($data, $config) = @_;
    $config->{exit} = 1;
}
